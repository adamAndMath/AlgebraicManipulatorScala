import Properties
import Numbers
import base.Add
import base.Sub
import base.Mult
import base.Div
import base.Pow
import base.Log
import Euler
using base.Log

define fn ln(Complex a) = log(e, a);

prove ide LnPow(Complex a) {
    let 2 ln(pow(e,a))
	unwrap:1
	substitute InverseLeft:1
} result {
	ln(pow(e,a))=a
}

prove ide PowLn(Complex a) {
	let 3 pow(e,ln(a))
	unwrap:1,1
	substitute InverseLeft:1
} result {
	pow(e,ln(a))=a
}

prove ide Inverse {
	let 2 ln(e)
	unwrap:1
	substitute Matching:1
} result {
	ln(e)=1
}

prove ide Singularity {
	let 2 ln(1)
	unwrap:1
	substitute SingularityRight:1
} result {
	ln(1)=0
}

prove ide Distribute(Complex a, Complex b) {
	let 2 log(e,mult(a,b))
	substitute DistributeMutLeft:1
	wrap ln:[0|1,[0|1]]
} result {
	ln(mult(a,b))=add(ln(a),ln(b))
}

prove ide DistributeSub(Complex a, Complex b) {
	let 2 log(e,div(a,b))
	substitute DistributeMutLeft:1
	wrap ln:[0|1,[0|1]]
} result {
	ln(div(a,b))=sub(ln(a),ln(b))
}

prove ide LnMult(Complex a, Complex p) {
	let 2 log(e,pow(a,p))
	substitute ~AssociateMut:1
	wrap ln:[0|1,0]
} result {
	ln(pow(a,p))=mult(ln(a),p)
}

prove ide ChangeBase(Complex a, Complex b) {
    let 2 log(a,b)
    substitute Log.ChangeBase(-,-,e):1
    wrap ln:1,[0|1]
} result {
    log(a,b)=div(ln(b),ln(a))
}