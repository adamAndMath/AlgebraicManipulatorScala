import Properties;
import Numbers;
import base.Add;
import base.Sub;
import base.Mult;
import base.Div;
import base.Pow;
using base.Add;
using base.Sub;
using base.Mult;
using base.Div;
using base.Pow;

inductive fn sum(Integer -> Complex f, Integer a, Integer b) {
    base b = a => f(a)
    step add(b,1) => add(sum(f,a,b),f(add(b,1)))
}

prove ide Single(Integer -> Complex f, Integer a) {
    let 2 sum(f,a,a)
    unwrap:1
} result {
    sum(f,a,a)=f(a)
}

prove ide SuccRight(Integer -> Complex f, Integer a, Integer b) {
    let 2 sum(f,a,add(b,1))
    unwrap:1
} result {
    sum(f,a,add(b,1))=add(sum(f,a,b),f(add(b,1)))
}

inductive ide PredLeft(Integer -> Complex f, Integer a, Integer b) {
    base b = a {
        let 2 sum(f,sub(a,1),a)
        substitute ~InverseRight(add,sub)(-,1):1,2
        substitute SuccRight:1
        substitute Single:1,0
        substitute ~Single:1,1
        substitute InverseRight:1,1,[1|2]
    } b -> add(b,1) {
        call x add(x,f(add(b,1)))
        substitute Associate:1
        substitute ~SuccRight:[0|1,1]
    } b -> sub(b,1) {
        substitute ~InverseRight(add,sub)(-,1):[0|1,1],2
        substitute SuccRight:[0|1,1]
        substitute InverseRight:[0|1,1],1,0
        substitute ~Associate:1
        call x sub(x,f(b))
        substitute InverseRight:[0|1]
    }
} result {
    sum(f,sub(a,1),b)=add(f(sub(a,1)),sum(f,a,b))
}

prove ide Identity(Integer -> Complex f, Integer a) {
    let 2 sum(f,a,sub(a,1))
    substitute ~InverseRight(sub,add)(-,f(a)):1
    substitute ~InverseRight(add,sub)(-,1):1,0,1,0
    substitute ~SuccRight:1,0
    substitute InverseRight:1,0,2
    substitute Single:1,0
    substitute Matching:1
} result {
    sum(f,a,sub(a,1))=0
}

inductive ide IndexShift<i>(Integer -> Complex f, Integer a, Integer b, Integer c) {
    base b = sub(a,1) {
        let 2 0
        substitute ~Identity(i -> f(add(i,c)),a):0
        substitute ~Identity(f,add(a,c)):1
        substitute ~OuterCommuteRight:1,2
    } b -> add(b,1) {
        call x add(x,f(add(add(b,c),1)))
        substitute OuterCommuteRight:0,1,0
        wrap i -> f(add(i,c)):0,1
        substitute ~SuccRight:[0|1]
        substitute OuterCommuteRight:1,2
    } b -> sub(b,1) {
        substitute ~InverseRight(add,sub)(-,1):[0|1],2
        substitute SuccRight:[0|1]
        substitute InverseRight:[0|1],1,0
        unwrap:0,1
        call x sub(x,f(add(b,c)))
        substitute InverseRight:[0|1]
        substitute ~OuterCommuteRight:1,2
    }
} result {
    sum(i -> f(add(i,c)),a,b)=sum(f,add(a,c),add(b,c))
}

prove ide IndexShiftSub<i>(Integer -> Complex f, Integer a, Integer b, Integer c) {
    let 2 sum(i -> f(sub(i,c)),a,b)
    substitute ~InverseRight(add,sub)(-,c):1,[1|2]
    substitute ~IndexShift<i>:1
    unwrap:1,0,0
    substitute InverseRight:1,0,0,0
    unwrap:1,0
} result {
    sum(i -> f(sub(i,c)),a,b)=sum(f,sub(a,c),sub(b,c))
}

inductive ide Reverse<i>(Integer -> Complex f, Integer a, Integer b) {
    base b=a {
        let 2 sum(f,a,a)
        substitute Single:1
        substitute ~NegateLeft(sub,-):1,0
        wrap i -> f(sub(0,i)):1
        substitute ~Single:1
    } b -> add(b,1) {
        call x add(x,f(add(b,1)))
        substitute Commute:1
        substitute ~SuccRight:0
        substitute ~NegateLeft(sub,-):1,0,0,0;
        substitute AssociateMut:1,0,0
        wrap i -> f(sub(0,i)):1,0
        substitute ~PredLeft:1
        substitute AssociateMut:1,1
    } b -> sub(b,1){
        substitute ~InverseRight(add,sub)(-,1):0,2
        substitute SuccRight:0;
        substitute ~InverseRight(sub,add)(-,1):1,1
        substitute PredLeft:1
        unwrap:1,0
        substitute Commute:1;
        substitute InverseRight:0,1,0
        substitute InverseRight:1,1,0,1
        substitute NegateLeft:1,1,0;
        substitute AssociateMut:1,0,1;
        call x sub(x,f(b));
        substitute InverseRight:[0|1];
    }
} result {
    sum(f,a,b)=sum(i -> f(sub(0,i)),sub(0,b),sub(0,a))
}

prove ide FixedReverse<i>(Integer -> Complex f, Integer a, Integer b) {
    let 2 sum(f,a,b)
    substitute Reverse<i>:1
    substitute ~Matching(sub,-)(a):1,1,0
    substitute ~Matching(sub,-)(b):1,2,0
    substitute AssociateMut:1,[1|2]
    substitute Commute:1,2,1
    substitute ~IndexShiftSub<i>:1
    unwrap:1,0,0
    substitute ~AssociateMut:1,0,0,0
    substitute OuterCommuteRight:1,0,0,0
    substitute IdentityLeft:1,0,0,0,0
} result {
    sum(f,a,b)=sum(i -> f(sub(add(a,b),i)),a,b)
}

inductive ide RangeSplit(Integer -> Complex f, Integer a, Integer b, Integer c) {
    base c=b {
        let 2 sum(f,a,b);
        substitute ~IdentityRight(add,-):1;
        substitute ~Identity(f,add(b,1)):1,1;
        substitute InverseRight:1,1,2;
    } c -> add(c,1) {
        call x add(x,f(add(c,1)));
        substitute Associate:1;
        substitute ~SuccRight:[0|1,1];
    } c -> sub(c,1){
        substitute ~InverseRight(add,sub)(-,1):[0|1,1],2;
        substitute SuccRight:[0|1,1];
        substitute InverseRight:[0|1,1],1,0;
        substitute ~Associate:1;
        call x sub(x,f(c));
        substitute InverseRight:[0|1];
    }
} result {
    sum(f,a,c)=add(sum(f,a,b),sum(f,add(b,1),c))
}

inductive ide Factor<i>(Integer -> Complex f, Complex c, Integer a, Integer b) {
    base b=a {
        let 2 sum(i -> mult(c,f(i)),a,a)
        substitute Single:0
        unwrap:0
        substitute ~Single:0,1
    } b -> add(b,1) {
        call x add(x,mult(c,f(add(b,1))))
        substitute ~DistributeLeft:0
        wrap i -> mult(c,f(i)):1,1
        substitute ~SuccRight:[0,1|1]
    } b -> sub(b,1) {
        substitute ~InverseRight(add,sub)(-,1):[0,1|1],2
        substitute SuccRight:[0,1|1]
        unwrap:1,1
        substitute InverseRight:[0|1],1,1,0
        substitute DistributeLeft:0
        call x sub(x,mult(c,f(b)))
        substitute InverseRight:[0|1]
    }
} result {
    mult(c,sum(f,a,b))=sum(i -> mult(c,f(i)),a,b)
}

inductive ide Distribute<i>(Integer -> Complex f, Integer -> Complex g, Integer a, Integer b) {
    base b=a {
        let 2 add(f(a),g(a));
        wrap i -> add(f(i),g(i)):0
        substitute ~Single:[0|1,[0|1]];
    } b -> add(b,1) {
        call x add(x,add(f(add(b,1)),g(add(b,1))))

        substitute ~Associate:1
        substitute OuterCommuteRight:1,0
        substitute Associate:1

        wrap i -> add(f(i),g(i)):0,1
        substitute ~SuccRight:[0|1,[0|1]]
    } b -> sub(b,1){
        substitute ~InverseRight(add,sub)(-,1):[0|1,[0|1]],2
        substitute SuccRight:[0|1,[0|1]]
        unwrap:0,1
        substitute InverseRight:[0,1,[0|1]|1,[0|1],1],0;

        substitute ~Associate:1;
        substitute OuterCommuteRight:1,0
        substitute Associate:1;

        call x sub(x,add(f(b),g(b)));
        substitute InverseRight:[0|1];
    }
} result {
    sum(i -> add(f(i),g(i)),a,b)=add(sum(f,a,b),sum(g,a,b))
}

prove ide DistributeSub<i>(Integer -> Complex f, Integer -> Complex g, Integer a, Integer b) {
    let 2 sum(i -> sub(f(i),g(i)),a,b)
    substitute ~InverseRight(sub,add)(-,sum(g,a,b)):1
    substitute ~Distribute<i>:1,0
    unwrap:1,0,0,0,0
    substitute InverseRight:1,0,0,0
    unwrap:1,0,0
} result {
    sum(i -> sub(f(i),g(i)),a,b)=sub(sum(f,a,b),sum(g,a,b))
}

inductive ide Commute<i,j>((Integer,Integer) -> Complex f, Integer a1, Integer b1, Integer a2, Integer b2) {
    base b1=a1 {
        let 2 sum(j -> f(a1,j),a2,b2)
        wrap i -> sum(j -> f(i,j),a2,b2):0
        wrap i -> f(i,j):1,0,0
        substitute ~Single:[0|1,0,0]
    } b1 -> add(b1,1) {
        call x add(x,sum(j -> f(add(b1,1),j),a2,b2))
        substitute ~Distribute<j>:1
        unwrap:1,0,0,[0|1]
        wrap i -> sum(j -> f(i,j),a2,b2):0,1
        wrap i -> f(i,j):1,0,0,1
        substitute ~SuccRight:[0|1,0,0]
    } b1 -> sub(b1,1){
        substitute ~InverseRight(add,sub)(-,1):[0|1,0,0],2
        substitute SuccRight:[0|1,0,0];
        substitute InverseRight:[0|1,0,0],1,0
        unwrap:[0|1,0,0],1
        wrap j -> sum(i -> f(i,j),a1,sub(b1,1)):1,0,0,0
        wrap j -> f(b1,j):1,0,0,1
        substitute Distribute:1;
        call x sub(x,sum(j -> f(b1,j),a2,b2));
        substitute InverseRight:[0|1];
    }
} result {
    sum(i -> sum(j -> f(i,j),a2,b2),a1,b1)=sum(j -> sum(i -> f(i,j),a1,b1),a2,b2)
}

inductive ide TriangleCommute<i,j>((Integer,Integer) -> Complex f, Integer a, Integer n) {
    base n=a {
        let 2 f(a,a)
        wrap j -> f(a,j):0
        wrap i -> f(i,a):1
        substitute ~Single:[0|1]
        wrap i -> sum(j -> f(i,j),a,i):0
        wrap j -> sum(i -> f(i,j),j,a):1
        substitute ~Single:[0|1]
    } n -> add(n,1) {
        call x add(x,sum(j -> f(add(n,1),j),a,add(n,1)))
        wrap i -> sum(j -> f(i,j),a,i):0,1
        substitute ~SuccRight:0
        substitute SuccRight:1,1
        substitute ~Associate:1
        substitute ~Distribute<j>:1,0
        unwrap:1,[0,0,0,[0|1]|1]
        wrap i -> f(i,j):1,0,0,0,1
        substitute ~SuccRight:1,0,0,0
        wrap i -> f(i,add(n,1)):1,1
        substitute ~Single:1,1;
        wrap j -> sum(i -> f(i,j),j,add(n,1)):1,1
        substitute ~SuccRight:1;
    } n -> sub(n,1){
        substitute ~InverseRight(add,sub)(-,1):[0|1,0,0],2
        substitute SuccRight:[0|1,0,0]
        substitute InverseRight:[0|1,0,0],1,0
        unwrap:[0|1,0,0],1
        wrap j -> sum(i -> f(i,j),j,sub(n,1)):1,0,0,0
        wrap j -> f(n,j):1,0,0,1
        substitute Distribute:1;
        substitute ~InverseRight(add,sub)(-,1):1,0,2
        substitute SuccRight:1,0;
        substitute InverseRight:1,0,1,0
        unwrap:1,0,1
        substitute Identity:1,0,1;
        substitute IdentityRight:1,0;

        call x sub(x,sum(j -> f(n,j),a,n));
        substitute InverseRight:[0|1];
    }
} result {
    sum(i -> sum(j -> f(i,j),a,i),a,n)=sum(j -> sum(i -> f(i,j),j,n),a,n)
}

/*induction Zip<i,j>(Integer -> Complex f, Integer n, Integer z) {
    base n = sub(0,1) {
        let 2 0
        substitute ~Identity(i -> sum(j -> f(add(mult(z,i),j)),0,z),0):0
        substitute ~Identity(f,add(mult(z,sub(0,1)),z)):1
        substitute Mult.DistributeSub:1,1,0
        substitute Mult.Singularity[2->0]:1,1,0,0
        substitute Mult.IdentityRight:1,1,0,1
        substitute ~Commute:1,1
        substitute ~Sub.AsAdd:1,1
        substitute Matching:1,1
    } n -> add(n,1) {
        call x add(x,sum(f,add(mult(z,n),z),sub(add(mult(z,add(n,1)),z),1)))
        wrap i -> f(i):0,1,0
        substitute ~InverseRight(sub,add)(-,add(mult(z,n),z)):0,1,0,0,0
        wrap j -> sub(j,add(mult(z,n),z)):0,1,0,0
        substitute ~IdentityLeft:0,1,1
        substitute ~Mult.DistributeSub:0,1,2,0
        substitute ~Mult.IdentityRight:0,1,2,0,1
        substitute AssociateMut:0,1,2
        substitute ~IndexShift:0,1
    }
} result {
    sum(i -> sum(j -> f(add(mult(z,i),j)),0,sub(z,1)),0,n)=sum(f,0,sub(add(mult(z,n),z),1))
}*/